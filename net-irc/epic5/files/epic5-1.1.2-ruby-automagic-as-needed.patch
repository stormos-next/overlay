From: Nathan Phillip Brink <binki@gentoo.org>
Subject: Fix up linking against libruby when using
	LDFLAGS=-Wl,--as-needed. Also allow ruby to be a non-automagic
	dependency.

--- a/configure.in
+++ b/configure.in
@@ -1191,34 +1191,32 @@
 dnl
 dnl Ruby support?
 dnl
-AC_ARG_WITH(ruby,
-[  --with-ruby[=PATH_TO_RUBY_EXE]       Compile with ruby support.],
-[ 
-	rubyexe=$withval
-],
-	rubyexe=yes
-)
-
-if test "x$rubyexe" = "xyes"; then
-	rubyexe=ruby
-fi
+AC_ARG_WITH([ruby],
+	[AS_HELP_STRING([--with-ruby[=PATH_TO_RUBY_EXE]], [Compile with ruby support.])],
+	[], [with_ruby=maybe])
 
+with_ruby_errormsg="--with-ruby was specified but I could not locate ruby. Please try specifying --with-ruby=/path/to/ruby or --without-ruby."
+AC_MSG_CHECKING([whether to support Ruby])
+if test "x$with_ruby" = "xno" ; then
-if test "x$rubyexe" = "xno"; then
-	AC_MSG_CHECKING(whether to support Ruby)
 	AC_MSG_RESULT(no)
 else
+	AC_MSG_RESULT(yes)
+	AS_IF([test "x$with_ruby" = "xyes" -o "x$with_ruby" = "xmaybe"],
+		[rubyexe=ruby],
+		[rubyexe="$with_ruby"])
+	dnl Support --with-ruby being passed a full path instead of just an executable name.
+	AS_IF([test -x "$rubyexe"],
+		[RUBYPROG=yes],
+		[AC_CHECK_PROG([RUBYPROG], [$rubyexe], [yes], [no])])
+	if test "x$RUBYPROG" = xno ; then
+		AS_IF([test "x$with_ruby" != "xmaybe"],
+			[AC_MSG_ERROR([$with_ruby_errormsg])])
-	AC_CHECK_PROG(RUBYPROG, $rubyexe, "yes", "no")
-	AC_MSG_CHECKING(whether to support Ruby)
-	if test $RUBYPROG != "yes" ; then 
-	    AC_MSG_RESULT(no)
 	else
-	    AC_MSG_RESULT(yes)
-
 	    incdir=`$rubyexe -rrbconfig -e 'puts Config::CONFIG[["archdir"]]'`
 	    rubylibs=`$rubyexe -rrbconfig -e 'puts Config::CONFIG[["LIBRUBYARG"]]'`
 	    extralibs=`$rubyexe -rrbconfig -e 'puts Config::CONFIG[["LIBS"]]'`
 	    RUBYCFLAGS="-I$incdir"
-	    RUBYLDFLAGS="$rubylibs $extralibs"
+	    RUBYLIBS="$rubylibs $extralibs"
 	    RUBYDOTOH="ruby.o"
 
 	    AC_MSG_CHECKING(whether embedded ruby works the way I expect)
@@ -1226,7 +1220,7 @@
 	    old_CFLAGS="$CFLAGS"
-	    old_LDFLAGS="$LDFLAGS"
+	    old_LIBS="$LIBS"
 	    CFLAGS="$CFLAGS $RUBYCFLAGS"
-	    LDFLAGS="$LDFLAGS $RUBYLDFLAGS"
+	    LIBS="$LDFLAGS $RUBYLIBS"
 	    AC_TRY_LINK([
 #include <ruby.h>
     VALUE epic_echo (VALUE module, VALUE string)
@@ -1251,9 +1245,9 @@
 		AC_MSG_RESULT(no)
 		AC_MSG_CHECKING(whether ruby requires -pthread to link)
 		RUBYCFLAGS="$RUBYCFLAGS -pthread"
-		RUBYLDFLAGS="$RUBYLDFLAGS -pthread"
+		RUBYLIBS="$RUBYLIBS -pthread"
-		CFLAGS="$CFLAGS $RUBYCFLAGS"
-		LDFLAGS="$LDFLAGS $RUBYLDFLAGS"
+		CFLAGS="$old_CFLAGS $RUBYCFLAGS"
+		LIBS="$old_LIBS $RUBYLIBS"
 
 	        AC_TRY_LINK([
 #include <ruby.h>
@@ -1279,16 +1273,18 @@
 
 	    CFLAGS="$old_CFLAGS"
-	    LDFLAGS="$old_LDFLAGS"
+	    LIBS="$old_LIBS"
 	    if test $have_embedded_ruby = "yes" ; then
 		AC_MSG_RESULT(yes)
 		AC_DEFINE(HAVE_RUBY)
 		AC_DEFINE(RUBYCFLAGS)
-		AC_DEFINE(RUBYLDFLAGS)
+		AC_DEFINE(RUBYLIBS)
 	    else
+		AS_IF([test "x$with_ruby" != "xmaybe"],
+			[AC_MSG_ERROR([$with_ruby_errormsg])])
 	        RUBYDOTOH=""
 		HAVE_RUBY=""
 		RUBYCFLAGS=""
-		RUBYLDFLAGS=""
+		RUBYLIBS=""
 		AC_MSG_RESULT(no, sorry)
 	    fi
 	fi
@@ -1386,7 +1380,7 @@
 AC_SUBST(LDFLAGS)
 AC_SUBST(RUBYDOTOH)
 AC_SUBST(RUBYCFLAGS)
-AC_SUBST(RUBYLDFLAGS)
+AC_SUBST(RUBYLIBS)
 AC_SUBST(TCLDOTOH)
 AC_SUBST(TCLCFLAGS)
 AC_SUBST(TCLLDFLAGS)

--- a/source/Makefile.in
+++ b/source/Makefile.in
@@ -38,7 +38,7 @@
 epic5: $(OBJECTS)
 	sh info.c.sh
 	$(CC) $(CFLAGS) $(INCLUDES) -c info.c
-	$(CC) $(CFLAGS) $(LDFLAGS) -o epic5 $(OBJECTS) info.o @PERLLDFLAGS@ @TCLLDFLAGS@ @RUBYLDFLAGS@ $(LIBS)
+	$(CC) $(CFLAGS) $(LDFLAGS) -o epic5 $(OBJECTS) info.o @PERLLDFLAGS@ @TCLLDFLAGS@ @RUBYLIBS@ $(LIBS)
 	$(RM) info.c info.o
 clean::
 	$(RM) epic5 $(OBJECTS)
